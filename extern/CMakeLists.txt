option(FAULT_BUNDLE_CPPTRACE "Bundle cpptrace into fault" ON)

set(TAG_PREFIX "v")
set(FAULT_CPPTRACE_MAX_TESTED_VERSION "1.0.4")
set(FAULT_CPPTRACE_TAG "${TAG_PREFIX}${FAULT_CPPTRACE_MAX_TESTED_VERSION}")

if(TARGET cpptrace::cpptrace)
    get_target_property(cpptrace_version cpptrace::cpptrace VERSION)
    message(STATUS "fault: Using existing cpptrace::cpptrace target (${cpptrace_version})")
    if (cpptrace_version AND cpptrace_version VERSION_GREATER FAULT_CPPTRACE_MAX_TESTED_VERSION)
        message(WARNING "fault: Existing cpptrace::cpptrace target (${cpptrace_version}) is newer than the maximum tested version ${FAULT_CPPTRACE_MAX_TESTED_VERSION}.")
    endif()
elseif(FAULT_BUNDLE_CPPTRACE)
    message(STATUS "fault: Bundling internal cpptrace via FetchContent")
    add_subdirectory(cpptrace EXCLUDE_FROM_ALL)
else()
    message(STATUS "fault: Searching for system-wide cpptrace")
    find_package(cpptrace CONFIG REQUIRED)
    if (NOT TARGET cpptrace::cpptrace)
        message(FATAL_ERROR "fault: Failed to find cpptrace::cpptrace target. Ensure that it is visible system-wide, or that you make it available before including fault, or that you enable FAULT_BUNDLE_CPPTRACE option")
    endif()
    get_target_property(cpptrace_version cpptrace::cpptrace VERSION)
    if (cpptrace_version AND cpptrace_version VERSION_GREATER FAULT_CPPTRACE_MAX_TESTED_VERSION)
        message(WARNING "fault: Found cpptrace::cpptrace target (${cpptrace_version}) is newer than the maximum tested version ${FAULT_CPPTRACE_MAX_TESTED_VERSION}.")
    endif()
endif()